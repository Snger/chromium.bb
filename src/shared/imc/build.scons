# -*- python -*-
# Copyright 2009 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.


Import('env')

if env.Bit('windows'):
    env.Append(CCFLAGS=['/EHsc'])

if env.Bit('linux'):
    env.Append(LIBS=['rt'])

nacl_imc_inputs = [
    'nacl_imc_common.cc',
]

if env.Bit('windows'):
  nacl_imc_inputs += [
    'win/nacl_imc.cc',
    'win/nacl_shm.cc',
  ]

if env.Bit('linux'):
  nacl_imc_inputs += [
    'nacl_imc_unistd.cc',
    'linux/nacl_imc.cc',
  ]

if env.Bit('mac'):
  nacl_imc_inputs += [
    'nacl_imc_unistd.cc',
    'osx/nacl_imc.cc',
  ]


common_objs = env.DualObject(nacl_imc_inputs)

nacl_imc_c_inputs = ['nacl_imc_c.cc']

env.DualLibrary('google_nacl_imc', common_objs)
env.DualLibrary('google_nacl_imc_c', common_objs + nacl_imc_c_inputs)

env.ComponentProgram('client',
                     'nacl_imc_test_client.cc',
                     EXTRA_LIBS=['google_nacl_imc', 'platform', 'gio'])

env.ComponentProgram('server',
                     'nacl_imc_test_server.cc',
                     EXTRA_LIBS=['google_nacl_imc', 'platform', 'gio'])


sigpipe_test_exe = env.ComponentProgram(
    'sigpipe_test',
    'sigpipe_test.cc',
    EXTRA_LIBS=['platform', 'gio', 'google_nacl_imc'])
node = env.CommandTest(
    'sigpipe_test.out',
    command=[sigpipe_test_exe])
env.AddNodeToTestSuite(node, ['small_tests'], 'run_sigpipe_test')
