# -*- python -*-
# Copyright 2010 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.

# This is a C PPAPI-based version of ../npapi_geturl.
#
# ppapi_geturl.html - test driver that loads the nexe and scripts it
# ppapi_geturl_success.html - to be url-loaded and displayed w/n driver html
#
# ppapi_geturl.cc - implementation of PPP interface
# module.h/cc - implementation of PPP_Instance interface
# scriptable_object.h/cc - implementation of the scripting interface
# url_load_request.h/cc - url loading helper
# nacl_file_main.cc - uses main() to test NaClFile interface

Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])

ppapi_geturl_nexe = env.ComponentProgram('ppapi_geturl.nexe',
                                         ['nacl_file_main.cc',
                                          'module.cc',
                                          'ppapi_geturl.cc',
                                          'scriptable_object.cc',
                                          'url_load_request.cc',
                                          ],
                                         EXTRA_LIBS=['nacl_file',
                                                     'ppruntime',
                                                     'imc',
                                                     'platform',
                                                     'gio',
                                                     'pthread',
                                                     'm',
                                                     'srpc'])

# Note that the html is required to run this program.
env.Publish('ppapi_geturl.nexe', 'run',
            ['ppapi_geturl.html',
             'ppapi_geturl_success.html',
             env.File('${SCONSTRUCT_DIR}/tests/nacl_js_lib.js') ])

node = env.BrowserTester(
    'ppapi_geturl_browser_test.out',
     url='ppapi_geturl.html',
     files=[ppapi_geturl_nexe,
            env.File('ppapi_geturl.html'),
            env.File('ppapi_geturl_success.html'),
            env.File('${SCONSTRUCT_DIR}/tests/nacl_js_lib.js'),
            ],
     )
env.AddNodeToTestSuite(node,
                       ['pepper_browser_tests'],
                       'run_ppapi_geturl_browser_test',
                       )

for suffix in ['valid', 'invalid']:
  basename = 'ppapi_geturl_' + suffix
  node = env.SelUniversalTest(
      basename + '.out',
      sel_universal_flags=['--command_file',
                           env.File('${SCONSTRUCT_DIR}/tests/ppapi_proxy'
                                    '/sel_universal_ppapi_replay_prolog.stdin'),
                           '--command_file',
                           env.File(basename + '.stdin'),
                           ],
      command=[env.File('ppapi_geturl.nexe')],
      stdout_golden=env.File(basename + '.stdout'),
      # filter out a line that contains unpredictable values
      filter_regex="'ScriptableObject::Call'",
      filter_inverse='true',
      )

  # Note: we use the sysv shm feature in sel_universal which is only available
  # on linux.
  is_broken = not env.Bit('host_linux')
  env.AddNodeToTestSuite(node,
                         ['small_tests', 'sel_ldr_tests'],
                         'run_' + basename + '_test',
                         is_broken=is_broken)
