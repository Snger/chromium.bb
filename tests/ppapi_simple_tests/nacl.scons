# -*- python -*-
# Copyright (c) 2011 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.


Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])

TESTS = [('audio', ('audio',)),
         ('graphics2d',('graphics2d',)),
         ('event', ('event',)),
         ('file', ('file1', 'file2')),
         ]

for test_exe, test_inputs in TESTS:
  nexe = env.ComponentProgram('ppapi_simple_' + test_exe + '.nexe',
                              [test_exe + '.cc'],
                              EXTRA_LIBS=['${PPAPI_LIBS}',
                                          'ppapi_cpp',
                                          'platform',
                                          'gio',
                                          'pthread',
                                          'm',
                                          ])

  for test in test_inputs:
    node = env.SelUniversalTest(
        'ppapi_simple_' + test + '.out',
        sel_universal_flags=[
            '--silence_nexe',
            '--command_file',
            env.File('sel_universal_ppapi_replay_prolog.stdin'),
            '--command_file',
            env.File(test +'.stdin'),
            ],
        uses_ppapi=True,
        command=[nexe],
        stdout_golden=env.File(test + '.stdout'),
        )

    env.AddNodeToTestSuite(node,
                           ['small_tests', 'sel_ldr_tests'],
                           'run_ppapi_simple_' + test  + '_test')
