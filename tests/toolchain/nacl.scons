# Copyright 2009 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.

Import('env')

# force inclusion of entire library, so that we can validate it
# NOTE: This approach does not work for -lc because of tons of
#       undefined symbols which would have to be stubbed out
wa_env = env.Clone()
if not ARGUMENTS.get('bitcode') and not env.Bit('nacl_glibc'):
  # NOTE: the pnacl linker does not understand '-Wl,-whole-archive'
  # c.f. http://code.google.com/p/nativeclient/issues/detail?id=943
  # NOTE: This test does not build successfully with nacl-glibc yet.
  wa_env.Prepend(LINKFLAGS=['-Wl,-whole-archive',
                            '-lgcc',
                            '-lm',
                            '-lnacl',
                            '-lsrpc',
                            '-lpthread',
                            '-Wl,-no-whole-archive'])

nexe = wa_env.ComponentProgram('dummy.nexe', 'dummy.c')
node = wa_env.CommandValidatorTestNacl('whole_archive_test.out',
                                       image=[nexe])
wa_env.AddNodeToTestSuite(node, ['toolchain_tests'], 'run_whole_archive_test')


if not ARGUMENTS.get('bitcode'):
  # c.f. http://code.google.com/p/nativeclient/issues/detail?id=516
  nexe = env.ComponentProgram('setlongjmp.nexe', 'setlongjmp.c')
  node = env.CommandSelLdrTestNacl('setlongjmp.out',
                                   command=[nexe],
                                   exit_status='55')
  env.AddNodeToTestSuite(node,
                         ['toolchain_tests','small_tests'],
                         'run_setlongjmp_test')

# NOTE: this currently only works for PNaCl, c.f:
# http://code.google.com/p/nativeclient/issues/detail?id=968
INITFINIT_TESTS=['toolchain_tests', 'small_tests']
if not ARGUMENTS.get('bitcode'):
  INITFINIT_TESTS=[]

nexe = env.ComponentProgram('initfini.nexe', 'initfini.c')
node = env.CommandSelLdrTestNacl('initfini.out',
                                 command=[nexe],
                                 stdout_golden=env.File('initfini.stdout'))
env.AddNodeToTestSuite(node, INITFINIT_TESTS, 'run_initfini_test')



sloppy_env = env.Clone()
sloppy_env.FilterOut(CFLAGS=['-pedantic', '-O2'])
sloppy_env.FilterOut(CCFLAGS=['-pedantic', '-O2'])
nexe = sloppy_env.ComponentProgram('return_address.nexe', 'return_address.c')
node = sloppy_env.CommandSelLdrTestNacl('return_address.out',
                                        command=[nexe],
                                        exit_status='55')
sloppy_env.AddNodeToTestSuite(node,
                              ['toolchain_tests', 'small_tests'],
                              'run_return_address_test')


eh_env = env.Clone()
if ARGUMENTS.get('bitcode'):
  # http://code.google.com/p/nativeclient/issues/detail?id=720
  eh_env.FilterOut(CFLAGS=['-O2'])
  eh_env.FilterOut(CCFLAGS=['-O2'])


for t in ['eh_virtual_dtor',
          'eh_loop_single',
          'eh_loop_many',
          'eh_catch_many',
          'eh_loop_break']:
  EH_TESTS=['toolchain_tests', 'small_tests']
  # http://code.google.com/p/nativeclient/issues/detail?id=720
  if ARGUMENTS.get('bitcode') and eh_env['TARGET_FULLARCH'] != 'x86-32':
      EH_TESTS=[]

  nexe = eh_env.ComponentProgram(t + '.nexe', t + '.cc')
  node = eh_env.CommandSelLdrTestNacl(t + '.out',
                                      command=[nexe],
                                      exit_status='55')
  eh_env.AddNodeToTestSuite(node, EH_TESTS, 'run_' + t + '_test')


eh_env2 = env.Clone()
eh_env2.FilterOut(CFLAGS=['-pedantic', '-O2'])
eh_env2.FilterOut(CCFLAGS=['-pedantic', '-O2'])
eh_env2.Append(CPPDEFINES=['TARGET_FULLARCH=${TARGET_FULLARCH}'])
# http://code.google.com/p/nativeclient/issues/detail?id=720
TRACE_TESTS=['toolchain_tests', 'small_tests']
if ARGUMENTS.get('bitcode') and eh_env2['TARGET_FULLARCH'] != 'x86-32':
  TRACE_TESTS=[]

# TODO(robertm): put a dummy Unwind_Backtrace into the pnacl TC
if  eh_env2['TARGET_FULLARCH'] != 'arm':
  nexe = eh_env2.ComponentProgram('unwind_trace.nexe', 'unwind_trace.cc')
  node = eh_env2.CommandSelLdrTestNacl('unwind_trace.out',
                                       command=[nexe],
                                       exit_status='55')
  eh_env2.AddNodeToTestSuite(node,
                             TRACE_TESTS,
                             'run_unwind_trace_test')

# NOTE: PNaCl does not even compile yet, o.w.
#       it seems reasonable to tie this the previous test
if not ARGUMENTS.get('bitcode'):
  nexe = eh_env2.ComponentProgram('stack_frame.nexe', 'stack_frame.cc')
  node = eh_env2.CommandSelLdrTestNacl('stack_frame.out',
                                       command=[nexe],
                                       exit_status='55')
  eh_env2.AddNodeToTestSuite(node,
                             TRACE_TESTS,
                             'run_stack_frame_test')
