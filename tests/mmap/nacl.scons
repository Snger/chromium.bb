# -*- python -*-
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

mmap_test_nexe = env.ComponentProgram('mmap_test',
                                      'mmap_test.cc',
                                      EXTRA_LIBS=['${PTHREAD_LIBS}',
                                                  '${NONIRT_LIBS}'])
node = env.CommandSelLdrTestNacl(
    'mmap_test.out',
    mmap_test_nexe,
    args=[env.File('test-file')],
    stdout_golden=env.File('mmap_test.stdout'),
    sel_ldr_flags=['-a'])

env.AddNodeToTestSuite(node,
                       ['small_tests', 'sel_ldr_tests'],
                       'run_mmap_test')

mmap_atomicity_nexe = env.ComponentProgram(
    'mmap_atomicity.nexe', 'mmap_atomicity.c',
    EXTRA_LIBS=['${PTHREAD_LIBS}', '${NONIRT_LIBS}'])
node = env.CommandSelLdrTestNacl(
    'mmap_atomicity_test.out', mmap_atomicity_nexe)
# * Multi-threading is oddly broken under ARM QEMU.  Our test's child
#   thread never runs.  It appears to be starved by the main thread.
# * This test is extremely slow on the memcheck bot, which eventually
#   times out, so it is disabled there.  This may be a consequence of
#   running under a VM, since running the test on memcheck locally is
#   fast enough.
is_broken = ((env.Bit('target_arm') and env.UsingEmulator())
             or env.Bit('running_on_valgrind'))
env.AddNodeToTestSuite(node, ['small_tests'], 'run_mmap_atomicity_test',
                       is_broken=is_broken)
