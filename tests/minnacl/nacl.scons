# -*- python -*-
# Copyright 2011 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.

Import('env')

# valgrind has problems with minimal_test_guest
if env.IsRunningUnderValgrind():
  Return()

# This test case tests an experimental profile of NaCl.  If you make a
# change which breaks this test which is difficult to fix, you can
# disable the test, but please CC mseaborn,krasin.

if 'TRUSTED_ENV' not in env:
  Return()
trusted_env = env['TRUSTED_ENV']

# This duplicates the library list in src/trusted/service_runtime/build.scons.
# We would not have to do this if we could get Scons to track dependencies
# between libraries.
VALIDATOR_LIBS = []
if trusted_env.Bit('target_x86'):
  VALIDATOR_LIBS += [trusted_env.NaClTargetArchSuffix('ncvalidate')]
elif trusted_env.Bit('target_arm'):
  VALIDATOR_LIBS += ['ncvalidate_arm_v2']

runner = trusted_env.ComponentProgram(
    'minimal_test_host', ['minimal_test_host.c'],
    EXTRA_LIBS=['sel',
                'sel_debug_dummy',
                'manifest_proxy',
                'simple_service',
                'thread_interface',
                'gio_wrapped_desc',
                'nonnacl_srpc',
                'nrd_xfer',
                'nacl_perf_counter',
                'nacl_base',
                'imc',
                'container',
                'nacl_fault_inject',
                'platform',
                'platform_qual_lib',
                'gio',
                ] + VALIDATOR_LIBS)


test_prog = env.ComponentProgram(
    'minimal_test_guest',
    ['minimal_test_guest.c'],
    EXTRA_LINKFLAGS=['-nostdlib'])

extra_deps = []
if env.Bit('pnacl_generate_pexe'):
  env.Replace(TRANSLATEFLAGS=['-nostdlib'])
  nexe_name, pexe_node = env.GetTranslatedNexe(test_prog)
  test_prog = nexe_name
  extra_deps = [pexe_node]

test_command = [runner, test_prog]
bootstrap, _ = env.GetBootstrap()
if bootstrap is not None:
  test_command = [bootstrap] + test_command

node = env.CommandTest('minimal_test.out', test_command,
                       stdout_golden=env.File('minimal_test.stdout'),
                       extra_deps=extra_deps)
env.AddNodeToTestSuite(node, ['small_tests'], 'run_minnacl_test',
                       is_broken=not env.Bit('nacl_static_link'))
