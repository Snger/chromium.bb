# -*- python -*-
# Copyright 2011 The Native Client Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# be found in the LICENSE file.

Import('env')

import sys

if 'TRUSTED_ENV' not in env:
  Return()
trusted_env = env['TRUSTED_ENV']

# The debug stub currently only works on x86-64.
if not env.Bit('target_x86_64'):
  Return()
# The test has to be able to kill the subprocess.
if sys.version_info < (2, 6):
  Return()
# Our test uses inline assembly.
if env.Bit('bitcode'):
  Return()

test_prog = env.ComponentProgram('debugger_test.nexe', 'debugger_test.c')

sel_ldr = trusted_env.File('${STAGING_DIR}/${PROGPREFIX}sel_ldr${PROGSUFFIX}')
env_vars = [
    'NACL_SEL_LDR=%s' % sel_ldr,
    'DEBUGGER_TEST_PROG=%s' % test_prog.abspath,
    ]
dependencies = [sel_ldr, test_prog]

# TODO(mlinck) Fix the test to correctly kill sel_ldr, and check if the
# port is already busy.  This appears to be related to Hardy which is
# why we are only turning this off on the coverage bots.
# See:
# http://code.google.com/p/nativeclient/issues/detail?id=1724
node = env.CommandTest(
    'debug_stub_test.out',
    command=['${PYTHON}', env.File('debug_stub_test.py'), '-v'],
    osenv=env_vars,
    extra_deps=dependencies)
env.AddNodeToTestSuite(node, ['large_tests'], 'run_debug_stub_test')
