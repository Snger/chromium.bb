# -*- python -*-
# Copyright (c) 2011 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# This test uses ppapi_test_lib.

Import('env')

env.Prepend(CPPDEFINES=['XP_UNIX'])

nexe = 'ppapi_ppb_instance_%s' % env.get('TARGET_FULLARCH')
env.Alias('ppapi_ppb_instance${PROGSUFFIX}', ['$STAGING_DIR/%s${PROGSUFFIX}' % nexe])

ppapi_ppb_instance_nexe = env.ComponentProgram(
    nexe,
    ['ppapi_ppb_instance.cc'],
    EXTRA_LIBS=['${PPAPI_LIBS}',
                'ppapi_test_lib',
                'pthread',
                'platform',
                'gio'])

# Note that the html is required to run this program.
dest_copy = env.Replicate('$STAGING_DIR',
                          ['ppapi_ppb_instance.html',
                           'ppapi_ppb_instance.nmf',
                           env.File('${SCONSTRUCT_DIR}/tools/browser_tester/'
                                    'browserdata/nacltest.js')]
                          )
env.Depends(nexe, dest_copy)

node = env.PPAPIBrowserTester('ppapi_ppb_instance_browser_test.out',
                              url='ppapi_ppb_instance.html',
                              files=[ppapi_ppb_instance_nexe,
                                     env.File('ppapi_ppb_instance.nmf'),
                                     env.File('ppapi_ppb_instance.html')])

env.AddNodeToTestSuite(node,
                       ['chrome_browser_tests'],
                       'run_ppapi_ppb_instance_browser_test',
                       is_broken=env.PPAPIBrowserTesterIsBroken())
