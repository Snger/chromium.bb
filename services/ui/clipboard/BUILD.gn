# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "clipboard_impl.cc",
    "clipboard_impl.h",
  ]

  deps = [
    "//base",
    "//mojo/common",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/tracing/public/cpp",
    "//services/ui/public/interfaces",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":mus_clipboard_unittests",
  ]
}

test("mus_clipboard_unittests") {
  sources = [
    "clipboard_unittest.cc",
    "run_all_unittests.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/common",
    "//mojo/edk/system",
    "//services/catalog:lib",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/service_manager/public/cpp:sources",
    "//services/ui/public/interfaces",
  ]

  data_deps = [
    ":lib",
    ":mus_clipboard_unittests_catalog_copy",
    "//services/ui",
  ]
}

service_manifest("test_manifest") {
  name = "mus_clipboard_unittests"
  source = "test_manifest.json"
}

catalog("mus_clipboard_unittests_catalog") {
  embedded_services = [ ":test_manifest" ]
  standalone_services = [ "//services/ui:manifest" ]
}

copy("mus_clipboard_unittests_catalog_copy") {
  sources = get_target_outputs(":mus_clipboard_unittests_catalog")
  outputs = [
    "${root_out_dir}/mus_clipboard_unittests_catalog.json",
  ]
  deps = [
    ":mus_clipboard_unittests_catalog",
  ]
}
