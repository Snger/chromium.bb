# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//services/catalog/public/tools/catalog.gni")
import("//services/service_manager/public/cpp/service.gni")
import("//services/service_manager/public/service_manifest.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "ime_registrar_impl.cc",
    "ime_registrar_impl.h",
    "ime_server_impl.cc",
    "ime_server_impl.h",
  ]

  deps = [
    "//base",
    "//mojo/common",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/tracing/public/cpp",
    "//services/ui/public/interfaces",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":mus_ime_unittests",
  ]
}

test("mus_ime_unittests") {
  sources = [
    "ime_unittest.cc",
    "run_all_unittests.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/common",
    "//mojo/edk/system",
    "//services/catalog:lib",
    "//services/service_manager/public/cpp:service_test_support",
    "//services/service_manager/public/cpp:sources",
    "//services/ui/public/interfaces",
  ]

  data_deps = [
    ":lib",
    ":mus_ime_unittests_catalog_copy",
    "//services/ui",
    "//services/ui/ime/test_ime_driver",
  ]
}

service_manifest("test_manifest") {
  name = "mus_ime_unittests"
  source = "test_manifest.json"
}

catalog("mus_ime_unittests_catalog") {
  embedded_services = [ ":test_manifest" ]
  standalone_services = [
    "//services/ui:manifest",
    "//services/ui/ime/test_ime_driver:manifest",
  ]
}

copy("mus_ime_unittests_catalog_copy") {
  sources = get_target_outputs(":mus_ime_unittests_catalog")
  outputs = [
    "${root_out_dir}/mus_ime_unittests_catalog.json",
  ]
  deps = [
    ":mus_ime_unittests_catalog",
  ]
}
