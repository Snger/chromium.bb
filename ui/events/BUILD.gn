# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

static_library("dom4_keycode_converter") {
  sources = [
    "keycodes/dom4/keycode_converter.cc",
    "keycodes/dom4/keycode_converter.h",
    "keycodes/dom4/keycode_converter_data.h",
  ]

  deps = [ "//base" ]
}

component("events_base") {
  sources = [
    "event_constants.h",
    "event_switches.cc",
    "event_switches.h",
    "events_base_export.h",
    "gesture_event_details.cc",
    "gesture_event_details.h",
    "keycodes/keyboard_code_conversion.cc",
    "keycodes/keyboard_code_conversion.h",
    "keycodes/keyboard_code_conversion_android.cc",
    "keycodes/keyboard_code_conversion_android.h",
    "keycodes/keyboard_code_conversion_mac.h",
    "keycodes/keyboard_code_conversion_mac.mm",
    "keycodes/keyboard_code_conversion_win.cc",
    "keycodes/keyboard_code_conversion_win.h",
    "keycodes/keyboard_code_conversion_x.cc",
    "keycodes/keyboard_code_conversion_x.h",
    "keycodes/keyboard_codes.h",
    "latency_info.cc",
    "latency_info.h",
    "x/device_data_manager.cc",
    "x/device_data_manager.h",
    "x/device_list_cache_x.cc",
    "x/device_list_cache_x.h",
    "x/touch_factory_x11.cc",
    "x/touch_factory_x11.h",
  ]

  defines = [ "EVENTS_BASE_IMPLEMENTATION" ]

  deps = [
    ":dom4_keycode_converter",
    "//base",
    "//base/third_party/dynamic_annotations",
    "//ui/gfx/geometry",
  ]

  if (use_x11) {
    configs += [ "//build/config/linux:x11" ]
  } else {
    sources -= [
      "keycodes/keyboard_code_conversion_x.cc",
      "keycodes/keyboard_code_conversion_x.h",
      "x/device_data_manager.cc",
      "x/device_data_manager.h",
      "x/device_list_cache_x.cc",
      "x/device_list_cache_x.h",
      "x/touch_factory_x11.cc",
      "x/touch_factory_x11.h",
    ]
  }
}

component("events") {
  deps = [
    ":dom4_keycode_converter",
    ":events_base",
  ]
}
