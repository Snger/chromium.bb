# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//testing/test.gni")

# Convenience meta-target for all of Blimp's production & test code.
group("blimp") {
  # In spite of the name, this really just means that non-test targets
  # cannot depend on this one, and that it can depend on test targets,
  # which fits how we intend to use it.
  testonly = true

  deps = [
    ":blimp_tests",
    "//blimp/common",
  ]
}

# Builds all Blimp test code (unit tests, test APKs).
group("blimp_tests") {
  testonly = true

  deps = [
    ":blimp_unittests",
  ]

  if (is_linux) {
    deps += [ "//blimp/test/fake_engine:fake_engine_app_tests" ]
  }
}

test("blimp_unittests") {
  deps = [
    "//blimp/common:unit_tests",
    "//blimp/net:unit_tests",
    "//blimp/test:run_all_unittests",
  ]

  if (is_android) {
    enable_multidex = true
  }
}
