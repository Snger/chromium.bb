# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")

# Applied by targets internal to content.
config("content_implementation") {
  defines = [ "CONTENT_IMPLEMENTATION" ]
}

content_shared_components = [
  "//content/gpu",
  "//content/plugin",
  "//content/ppapi_plugin",
  "//content/public/app",
  "//content/public/browser:sources",
  "//content/public/child",
  "//content/public/common",
  "//content/public/plugin",
  "//content/public/renderer",
  "//content/renderer",
  "//content/utility",
]

if (is_component_build) {
  shared_library("content") {
    deps = content_shared_components + [
      "//content/app",
    ]
    forward_dependent_configs_from = deps
  }
} else {
  group("content") {
    deps = content_shared_components
  }
}

grit("resources") {
  source = "content_resources.grd"
  use_qualified_include = true
  outputs = [
    "grit/content_resources.h",
    "content_resources.pak",
    "content_resources.rc",
  ]
}

# This target exists to "hold" the content_export header so we can do proper
# inclusion testing of it.
source_set("export") {
  visibility = [ "//content/*" ]
  sources = [
    "content/common/content_export.h"
  ]
}
